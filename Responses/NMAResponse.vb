Imports System.Net.Http

Namespace NotifyMyAndroid

    ''' <summary>
    ''' This is the base class for response messages generated by the NMA API.
    ''' </summary>
    Public MustInherit Class NMAResponse

        Private _source As XDocument
        Protected Sub New(response As XDocument)
            _source = response
        End Sub

        ''' <summary>
        ''' Indicates the status code associated with the request that generated this response.
        ''' </summary>
        Public ReadOnly Property StatusCode As StatusCode
            Get
                Return Me.GetAttribute(Of StatusCode)("code")
            End Get
        End Property

        ''' <summary>
        ''' Indicates the amount of minutes until the remaining amount of API calls resets.
        ''' </summary>
        Public ReadOnly Property TimeUntilReset As TimeSpan?
            Get
                If Me.StatusCode = NotifyMyAndroid.StatusCode.Success Or Me.StatusCode = NotifyMyAndroid.StatusCode.LimitReached Then
                    Return Me.GetAttribute(Of TimeSpan)("resettimer")
                End If
            End Get
        End Property

        ''' <summary>
        ''' Gets whether the status code indicates success.
        ''' </summary>
        Public ReadOnly Property IsSuccessStatusCode As Boolean
            Get
                Return Me.StatusCode = NotifyMyAndroid.StatusCode.Success
            End Get
        End Property

        ''' <summary>
        ''' Throws a new <see cref="NMAException"/> if the status code indicates failure.
        ''' </summary>
        Public Sub EnsureSuccessStatusCode()
            If Not Me.IsSuccessStatusCode Then
                Throw New NMAException(Me.StatusCode, Me.GetElement().Value)
            End If
        End Sub

        ''' <summary>
        ''' Gets an instance of <see cref="NMASuccess"/> or <see cref="NMAError"/> for the specified response.
        ''' </summary>
        Public Shared Function GetResponse(response As XDocument) As NMAResponse
            If response Is Nothing Then
                Throw New ArgumentNullException("response")
            End If
            Dim result = TryCast(response.Root.FirstNode, XElement)
            If result Is Nothing Then
                Throw New ArgumentException("specified response is invalid", "response")
            End If
            Select Case True
                Case String.Equals(result.Name.LocalName, "success", StringComparison.OrdinalIgnoreCase)
                    Return New NMASuccess(response)
                Case String.Equals(result.Name.LocalName, "error", StringComparison.OrdinalIgnoreCase)
                    Return New NMAError(response)
                Case Else
                    Throw New ArgumentException("specified response is invalid", "response")
            End Select
        End Function

#Region "Implementation"

        Protected ReadOnly Property Source As XDocument
            Get
                Return New XDocument(_source)
            End Get
        End Property

        Protected Function GetElement() As XElement
            Return DirectCast(Me.Source.Root.FirstNode, XElement)
        End Function

        Protected Function HasAttribute(name As String) As Boolean
            Return Me.GetElement().Attribute(name) IsNot Nothing
        End Function

        Protected Function GetAttribute(Of T)(name As String) As T
            Dim value As String = Me.GetElement().Attribute(name).Value
            Select Case True
                Case GetType(T) Is GetType(String)
                    Return DirectCast(Convert.ChangeType(value, GetType(T)), T)
                Case GetType(T) Is GetType(Integer)
                    Return DirectCast(Convert.ChangeType(Integer.Parse(value), GetType(T)), T)
                Case GetType(T) Is GetType(TimeSpan)
                    Return DirectCast(Convert.ChangeType(TimeSpan.FromMinutes(Integer.Parse(value)), GetType(T)), T)
                Case GetType(T) Is GetType(StatusCode)
                    Return DirectCast(Convert.ChangeType([Enum].Parse(GetType(StatusCode), value), GetType(T)), T)
            End Select
        End Function

#End Region

    End Class

End Namespace